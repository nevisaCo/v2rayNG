plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion Integer.parseInt("$compileSdkVer")
    buildToolsVersion "$buildToolsVer"

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    //auto increment version code
    def Properties versionProps = new Properties()
    def versionPropsFile = file('version.properties')
    if(versionPropsFile.exists())
        versionProps.load(new FileInputStream(versionPropsFile))
    def code = (versionProps['VERSION_CODE'] ?: "0").toInteger() + 1
    versionProps['VERSION_CODE'] = code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    defaultConfig {
        applicationId PACKAGE_NAME
        minSdkVersion 21
        targetSdkVersion Integer.parseInt("$targetSdkVer")
        multiDexEnabled true
        versionCode code
        versionName "1.0." + code
        ndk.abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'

        manifestPlaceholders = [
                admobAppId     : ADMOB_APPLICATION_ID,
                admobAppIdDebug: "ca-app-pub-3940256099942544~3347511713"
        ]

        buildConfigField "String", "FLURRY_APP_ID", FLURRY_APP_ID
        buildConfigField "String", "API_URL", API_URL
        buildConfigField "String", "PRIVACY_URL", PRIVACY_URL

    }


    signingConfigs {
        debug {
            storeFile file("/config/key.jks")
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }

        release {
            storeFile file("/config/key.jks")
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }


    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            zipAlignEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'Boolean', 'DEBUG_VERSION', "false"
        }
        debug {
            debuggable true
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
            buildConfigField 'Boolean', 'DEBUG_VERSION', "true"
            applicationIdSuffix ".debug"
        }
    }

    sourceSets.debug {
        manifest.srcFile '../app/config/debug/AndroidManifest.xml'
    }

    flavorDimensions "minApi"
    productFlavors {
        armv7 {
            ndk {
                abiFilters "armeabi-v7a"
            }

        }
        x86 {
            ndk {
                abiFilters "x86"
            }
        }
        arm64 {
            ndk {
                abiFilters "arm64-v8a"
            }
        }
        x64 {
            ndk {
                abiFilters "x86_64"
            }
        }
        afat {
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            java.srcDirs += 'src/main/kotlin'
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a' //select ABIs to build APKs for
            universalApk true //generate an additional APK that contains all the ABIs
        }
    }

    // map for the version code
    project.ext.versionCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, 'x86': 3, 'x86_64': 4]

     android.applicationVariants.all { variant ->
         // assign different version code for each output
         variant.outputs.each { output ->
             output.outputFileName = PACKAGE_NAME + variant.versionName + "_" + output.getFilter(com.android.build.OutputFile.ABI) + ".apk"

          /*   output.versionCodeOverride =
                     project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) *
                             1000000 + android.defaultConfig.versionCode*/
         }
     }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'], exclude: [])
    testImplementation 'junit:junit:4.13.2'

    // Androidx
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-beta01'

    // Androidx ktx
    implementation 'androidx.activity:activity-ktx:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'

    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.3"

    implementation 'com.tencent:mmkv-static:1.2.12'//key value db
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'io.reactivex:rxjava:1.3.4'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.4@aar'
    implementation 'me.dm7.barcodescanner:core:1.9.8'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'com.github.jorgecastilloprz:fabprogresscircle:1.01@aar'
    implementation 'me.drakeet.support:toastcompat:1.1.0'
    implementation 'com.blacksquircle.ui:editorkit:2.1.1'
    implementation 'com.blacksquircle.ui:language-base:2.1.1'
    implementation 'com.blacksquircle.ui:language-json:2.1.1'

    implementation 'com.github.florent37:viewanimator:1.1.0' //view animation

    //firebase
    implementation platform('com.google.firebase:firebase-bom:31.1.1')
//    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.android.gms:play-services-ads:21.4.0'

    implementation 'com.flurry.android:analytics:14.0.0@aar'

    implementation 'com.android.volley:volley:1.2.1'

    implementation 'com.makeramen:roundedimageview:2.3.0'

    implementation 'com.google.android.play:app-update:2.0.1'//google play in-app auto update

    implementation 'com.squareup.picasso:picasso:2.71828'
}

//buildscript {
//    repositories {
//        google()
//        mavenCentral()
//        maven { url 'https://maven.google.com' }
//        maven { url 'https://jitpack.io' }
//    }
//}